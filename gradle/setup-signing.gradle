/**
 * Setups up the necessary gradle properties to be able to sign the
 * sdk aar files. Should be applied to the end of each bvsdk module
 * build.gradle file,
 * e.g. apply from: rootProject.file('gradle/setup-signing.gradle')
 */
apply plugin: 'nu.studer.credentials'
apply plugin: 'signing'

def setupSigning() {
    def hasId = rootProject.hasProperty("signing.keyId") && rootProject."signing.keyId" != null
    def hasSecringFile = rootProject.hasProperty("signing.secretKeyRingFile") && rootProject."signing.secretKeyRingFile" != null
    def hasPassword = credentials.forKey("signing.password") != null

    logger.debug('Setup Signing: hasId? ' + hasId + ', hasSecringFile?' + hasSecringFile + ', hasPassword? ' + hasPassword)
    if (!hasId || !hasSecringFile || !hasPassword) {
        logger.debug('Setup Signing: Not setting signing values because one or more values is missing')
        return
    }

    def id = rootProject."signing.keyId"
    def file = System.getenv('HOME') + '/' + rootProject."signing.secretKeyRingFile"
    def password = credentials.forKey("signing.password")

    allprojects { ext."signing.keyId" = id }
    allprojects { ext."signing.secretKeyRingFile" = file }
    allprojects { ext."signing.password" = password }
}

setupSigning()