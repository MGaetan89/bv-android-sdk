repositories {
  mavenLocal()
  jcenter()
  mavenCentral()
  flatDir {
    dirs 'libs'
  }
}

apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

def hasCrashlyticsKey() {
  return file('fabric.properties').exists()
}

if (hasCrashlyticsKey()) {
  apply plugin: 'io.fabric'
}

def hasAppdemoBuildSettings() {
  return hasProperty('BVSDK_DEMOAPP_STORE_FILE') && file(BVSDK_DEMOAPP_STORE_FILE).exists() && hasProperty('BVSDK_DEMOAPP_STORE_PASSWORD') && hasProperty('BVSDK_DEMOAPP_KEY_ALIAS') && hasProperty('BVSDK_DEMOAPP_KEY_PASSWORD');
}

android {
  compileSdkVersion demoAppBuildInfo.compileSdkVersion
  buildToolsVersion demoAppBuildInfo.buildToolsVersion

  compileOptions {
    sourceCompatibility demoAppBuildInfo.sourceCompatibilityVersion
    targetCompatibility demoAppBuildInfo.targetCompatibilityVersion
  }

  dexOptions {
    // Skip pre-dexing when running on CI server or when disabled via -Dpre-dex=false.
    preDexLibraries = preDexEnabled && !isCi
  }

  packagingOptions {
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/DEPENDENCIES'
  }

  signingConfigs {
    release {
      if (hasAppdemoBuildSettings()) {
        storeFile file(BVSDK_DEMOAPP_STORE_FILE)
        storePassword BVSDK_DEMOAPP_STORE_PASSWORD
        keyAlias BVSDK_DEMOAPP_KEY_ALIAS
        keyPassword BVSDK_DEMOAPP_KEY_PASSWORD
      }
    }
  }

  defaultConfig {
    minSdkVersion demoAppBuildInfo.minSdkVersion
    targetSdkVersion demoAppBuildInfo.targetSdkVersion
    versionCode demoAppVersionCode
    versionName demoAppVersionName
    vectorDrawables.useSupportLibrary = true
  }

    buildTypes {
        release {
            minifyEnabled false
            if (hasAppdemoBuildSettings()) {
                signingConfig signingConfigs.release
            }
            signingConfig signingConfigs.release
            debuggable true
            buildConfigField "boolean", "ENABLE_DRY_RUN_ANALYTICS", "false"
        }
        debug {
            applicationIdSuffix '.debug'
            debuggable true
            buildConfigField "boolean", "ENABLE_DRY_RUN_ANALYTICS", "true"
        }
    }

  buildTypes.each {
    it.buildConfigField 'boolean', 'HAS_CRASHLYTICS_KEY', String.valueOf(hasCrashlyticsKey())
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  lintOptions {
    abortOnError false
  }
}

dependencies {
    if (useLocalSdk) {
        implementation project(':bvanalytics')
        implementation project(':bvauthiovation')
        implementation project(':bvrecommendations')
        implementation project(':bvconversations')
        implementation project(':bvconversationsui')
        implementation project(':bvcurations')
        implementation project(':bvlocation')
        implementation project(':bvstorenotifications')
        implementation project(':bvcurationsui')
        compileOnly project(':bvnotifications')
        compileOnly project(':bvcommon')
    } else {
        implementation demoAppDep.bvSdkAnalytics
        implementation demoAppDep.bvSdkAuthIovation
        implementation('com.iovation.fraudforce.lib.FraudForce:fraudforce-lib-release-3.0.1:3.0.1@aar')
        implementation demoAppDep.bvSdkRecommendations
        implementation demoAppDep.bvSdkConversations
        implementation demoAppDep.bvSdkCurations
        implementation demoAppDep.bvSdkStoreNotif
        implementation demoAppDep.bvSdkCurationsUi
        implementation demoAppDep.bvSdkConversationsUi
    }
    implementation demoAppDep.picasso
    implementation demoAppDep.picassoDownloader
    implementation demoAppDep.gson
    implementation demoAppDep.okhttp3
    implementation demoAppDep.okhttp3Logging
    implementation demoAppDep.prettyTime
    implementation demoAppDep.appCompatV7
    implementation demoAppDep.designCompat
    implementation demoAppDep.supportV4
    implementation demoAppDep.cardview
    implementation demoAppDep.preferenceV7
    implementation demoAppDep.preferenceV14
    implementation demoAppDep.constraintLayout
    implementation demoAppDep.playServiceAds
    implementation demoAppDep.playServiceMaps
    implementation demoAppDep.playServiceLocation
    implementation demoAppDep.dagger
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    annotationProcessor demoAppDep.daggerProcessor
    implementation demoAppDep.butterknife
    implementation demoAppDep.processPhoenix
    annotationProcessor demoAppDep.butterknifeCompiler
    releaseImplementation demoAppDep.leakCanaryNoOp
    debugImplementation demoAppDep.leakCanary
    testImplementation demoAppTestDep.junit
    implementation(demoAppDep.crashlytics) {
        transitive = true;
    }
}




