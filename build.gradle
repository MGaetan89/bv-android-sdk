buildscript {
    ext.kotlin_version = '1.8.10'
    apply from: rootProject.file('dependencies.gradle')

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:7.4.0'
    classpath sdkBuildDep.dexCountPlugin
    classpath sdkBuildDep.credentialsPlugin
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.kotlin:kotlin-android-extensions:$kotlin_version"
  }
}

plugins {
  id "org.sonarqube" version "3.3"
}

subprojects {
  def isRootProject = rootProject.name.equals(name)

  repositories {
    if (rootProject.hasProperty('NEXUS_USER') &&
        rootProject.hasProperty('NEXUS_PASSWORD')) {
      maven {
        url "https://repo.bazaarvoice.com/nexus/content/repositories/snapshots"
        credentials {
          username NEXUS_USER
          password NEXUS_PASSWORD
        }
      }
    }
    google()
    mavenCentral()
  }

  if (!isRootProject) {
    plugins.apply('checkstyle')

    task('checkstyle', type: Checkstyle) {
      configFile rootProject.file('checkstyle.xml')
      source 'src/main/java'
      ignoreFailures false
      showViolations true
      include '**/*.java'

      classpath = files()
    }
  }

  group = GROUP
  version = VERSION_NAME

  afterEvaluate {
    if (!isRootProject) {
      tasks.findByName('check').dependsOn('checkstyle')
    }
  }
}
